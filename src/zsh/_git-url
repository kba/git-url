#compdef git-url

typeset -A opt_args

function __tmux-sessions() {
    local -a sessions
    sessions=( ${${(f)"$(command tmux 2> /dev/null list-sessions)"}/:[ $'\t']##/:} )
    _describe -t sessions 'sessions' sessions "$@"
}

function __git-url-complete() {
    local group_name curcontext="$curcontext"
    group_name="$@[-1]"
    shift -p
    local -a xyz
    curcontext="${curcontext}:git-url-$group_name"
    xyz=(${(f)"$(git-url zsh-complete $group_name)"})
    _describe -t "$group_name" "$group_name" xyz "$@"
    # echo "$(date): ${curcontext}" >> /tmp/cpldebug
}

function _git-url() {
    local line
    _arguments -C \
        ${(f)"$(git-url zsh-complete options)"} \
        "1:command:__git-url-complete commands" \
        "2:argument:->args" \
    && ret=0

    case $state in
        (args)
            # tags=('sessions' 'repos' 'urls' 'options' 'option_names' 'command_names' 'commands' 'tags')
            # _tags ${tags[@]}
            case $line[1] in
                (tmux)
                    _alternative \
                        "sessions:Active tmux sessions:__tmux-sessions" \
                        "repos:Local Repositories:__git-url-complete repos" \
                        "urls:URLs:_urls" \
                        && ret=0
                    ;;
                (usage)
                    _alternative \
                        "tags:Tags:__git-url-complete tags" \
                        && ret=0
                    ;;
                (help)
                    _alternative \
                        "tags:Tags:__git-url-complete tags" \
                        "option_names:Help on options:__git-url-complete option_names" \
                        "command_names:command_names:__git-url-complete command_names" \
                        && ret=0
                    ;;
                (zsh-complete)
                    _alternative \
                        "zsh-complete:Completion groups:__git-url-complete zsh-complete" \
                        && ret=0
                    ;;
                (*)
                    _alternative \
                        "repos:Local Repositories:__git-url-complete repos" \
                        "urls:URLs:_urls" \
                        && ret=0
                    ;;
            esac
        ;;
    esac
}

_git-url "$@"

zstyle ':completion::*:repos' list-colors '=(#b)(*)/(*)/(*)==1;31=1;35=1;34'

# vim:ft=zsh:
